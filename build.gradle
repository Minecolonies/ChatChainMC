buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

plugins {
    id 'io.franzbecker.gradle-lombok' version '1.12'
    id 'net.ltgt.apt' version '0.10'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: "net.minecraftforge.gradle.forge"

group = modGroup
archivesBaseName = modBaseName
version = System.getenv()['BUILD_NUMBER'] != null ? System.getenv()['BUILD_NUMBER'] : System.getenv()['TRAVIS_BUILD_NUMBER']
mainClassName = 'co.chatchain.mc.forge.ChatChainMC'

sourceSets {
    api {
        java {
            srcDir 'src/api/java'
        }
    }
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
        }
        compileClasspath += sourceSets.api.output
    }
}

minecraft {
    version = project.forgeVersion
    runDir = "run"

    mappings = project.mcpVersion
}

repositories {
    ivy {
        name 'Minecolonies TeamCity Repo'
        ivyPattern 'http://teamcity.minecolonies.com/guestAuth/repository/download/[module]/[revision]/teamcity-ivy.xml'
        artifactPattern 'http://teamcity.minecolonies.com/guestAuth/repository/download/[module]/[revision]/[artifact](.[ext])'
    }
    mavenCentral()
    jcenter()
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
}

dependencies {
    compile('org.jetbrains:annotations:17.0.0')

    compile('org.projectlombok:lombok:1.16.18')
    apt('org.projectlombok:lombok:1.16.18')

    compile('ninja.leaping.configurate:configurate-gson:3.3')

    compile('org.java-websocket:Java-WebSocket:1.3.4')

    compileOnly('org.spongepowered:spongeapi:' + spongeApiVersion)
    
    compile 'io.reactivex.rxjava2:rxjava:2.2.2'

    compile('org:ChatChain_ChatChainCommons_Branches:' + chatChainCommonsVersion) {
        artifact {
            name = 'ChatChainCommons-' + chatChainCommonsVersion + '-all'
            type = 'jar'
        }
        artifact {
            name = 'ChatChainCommons-' + chatChainCommonsVersion + '-sources'
            type = 'jar'
            classifier = "sources"
        }
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        expand "version": project.version, "mcversion": project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

shadowJar {
    from sourceSets.api.output
    from sourceSets.main.output

    exclude 'dummyThing'

    exclude 'META-INF'
    relocate('com.typesafe.config', 'configurate.typesafe.config')
}

reobf {
    shadowJar {}
}
